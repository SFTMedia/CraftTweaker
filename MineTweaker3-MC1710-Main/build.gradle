import minetweaker.tasks.RegisterZenClassesTask;

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
    }
}

apply plugin: 'forge'
apply plugin: 'maven'

version = "1.7.10-" + ext.mineTweakerVersion
archivesBaseName = project.projectDir.name

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
	version = forgeVersion1710
}

dependencies {
	dependencies {
		compile project(':ZenScript')
		compile project(':MineTweaker3-API')
	}
}

task makeRegistry(type: RegisterZenClassesTask) {
	inputDir = file('build/classes/java/main')
	outputDir = file('build/classes/java/main')
	className = 'minetweaker.mc1710.MineTweakerRegistry'
}

tasks.jar.dependsOn(makeRegistry)

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
	
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + System.getenv("local_maven"))
            pom {
                groupId = project.group
                version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'CraftTweaker'
                    url 'https://github.com/jaredlll08/CraftTweaker'
                    scm {
                        url 'https://github.com/jaredlll08/CraftTweaker.git'
                        connection 'scm:git:git@github.com:jaredlll08/CraftTweaker.git'
                        developerConnection 'scm:git:git@github.com:jaredlll08/CraftTweaker.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/jaredlll08/CraftTweaker/issues'
                    }
                    developers {
                        developer {
                            id 'Jared'
                            name 'Jared'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}
